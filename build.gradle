plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.14'
}

group = 'org.example'
version = '1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.controlsfx:controlsfx:11.2.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'

    implementation 'io.rest-assured:rest-assured:5.3.1'

    implementation 'org.apache.logging.log4j:log4j-core:2.21.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.21.0'
}


javafx {
    version = '20'
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

application {
    mainClass = 'org.example.MainApp'
}

test {
    useJUnitPlatform()
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.author = true
    options.version = true
    options.links("https://docs.oracle.com/en/java/javase/17/docs/api/")
}


tasks.register('fatJar', Jar) {

    group = 'build'
    archiveClassifier.set('all')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes(
                'Main-Class': 'org.example.MainApp'
        )
    }

    from(sourceSets.main.output)

    dependsOn(configurations.runtimeClasspath)

    from {
        configurations.runtimeClasspath
                .findAll { it.name.endsWith('jar') }
                .collect { zipTree(it) }
    }
}

